мат_функ = імпорт("!/математика/функції.борщ");

__експортовані__ = [
    "сортування_вибором",
    "сортування_вставкою",
    "сортування_злиттям"
];

функція сортування_вибором(набір: список): список {
    кількість = довжина(набір);
    цикл (і : 0 .. кількість) {
        мін_індекс = і;
        цикл (ж : і + 1 .. кількість) {
            якщо (набір[мін_індекс] > набір[ж]) {
                мін_індекс = ж;
            }
        }

        набір[і], набір[мін_індекс] = набір[мін_індекс], набір[і];
    }

    повернути набір;
}

функція сортування_вставкою(набір: список): список {
    кількість = довжина(набір);
    цикл (і : 1 .. кількість) {
        поточний = набір[і];
        ж = і - 1;

        // Пересуваємо елементи масиву 'набір[0..i-1]', які є
        // більшими за 'поточний', на одну позицію вперед відносно
        // їх поточної позиції.
        цикл (ж >= 0 && поточний < набір[ж]) {
            набір[ж + 1] = набір[ж];
            ж = ж - 1;
        }

        набір[ж + 1] = поточний;
    }

    повернути набір;
}

функція _злити(перший: список, другий: список): список  {
    результат = [];
    розмір_першого = довжина(перший);
    розмір_другого = довжина(другий);
    і, к = 0, 0;
    цикл (і < розмір_першого && к < розмір_другого) {
        якщо (перший[і] < другий[к]) {
            результат = додати(результат, перший[і]);
            і = і + 1;
        }
        інакше {
            результат = додати(результат, другий[к]);
            к = к + 1;
        }
    }

    цикл (і < розмір_першого) {
        результат = додати(результат, перший[і]);
        і = і + 1;
    }

    цикл (к < розмір_другого) {
        результат = додати(результат, другий[к]);
        к = к + 1;
    }

    повернути результат;
}

функція сортування_злиттям(набір: список): список {
    кількість = довжина(набір);
    якщо (кількість < 2) {
        повернути набір;
    }

    середина = мат_функ.підлога(кількість / 2);
    лівий = сортування_злиттям(набір[0:середина]);
    правий = сортування_злиттям(набір[середина:]);
    повернути _злити(лівий, правий);
}
