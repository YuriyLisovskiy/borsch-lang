
__експортовані__ = ["Стек"];

/**
 Стек — це лінійна структура даних, яка зберігає елементи
 за принципом "останній прийшов/перший вийшов". Новий елемент
 у стек додають з одного кінця, а видаляють з іншого.
*/
клас Стек

    __документ__ = "";

    функція __конструктор__(я: Стек)
        я._список = [];
    кінець;

    функція __довжина__(я: Стек): ціле
        повернути я.розмір();
    кінець;

    функція __рядок__(я: Стек): рядок
        повернути рядок(я._список);
    кінець;

    /**
     Часова складність: O(1)
    */
    функція порожній(я: Стек): логічне
        повернути я.розмір() == 0;
    кінець;

    /**
     Часова складність: O(1)
    */
    функція розмір(я: Стек): ціле
        повернути довжина(я._список);
    кінець;

    /**
     Повертає верхній елемент стеку.

     Часова складність: O(1)
    */
    функція вершина(я: Стек): довільний
        якщо (я.порожній())
            панікувати Помилка("стек порожній");
        кінець;

        повернути я._список[-1];
    кінець;

    /**
     Вставляє елемент у верхню частину стеку.

     Часова складність: O(1)
    */
    функція додати(я: Стек, елемент: довільний)
        я._список = додати(я._список, елемент);
        повернути нуль;
    кінець;

    /**
     Видаляє верхній елемент стеку.

     Часова складність: O(1)
    */
    функція вилучити(я: Стек): довільний
        елемент = я.вершина();
        я._список = я._список[0:-1];
        повернути елемент;
    кінець;

кінець;
